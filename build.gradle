buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-alpha5'
    }
}

apply plugin: 'com.android.application'

def getDate() {
    return Calendar.getInstance().getTimeInMillis()
}

repositories {
    mavenCentral()
}


dependencies {
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.google.guava:guava:19.0-rc2'
    compile project(':androidwversionmanager')
    compile 'org.thoughtcrime.ssl.pinning:AndroidPinning:1.0.0'
}

android {

    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 14
        versionName "3.0.1"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

    }

    signingConfigs {

        debug {
            storeFile file("${System.properties['user.home']}" +  "/.android/debug.keystore")
        }

        /* note:  To build a signed version of nethunter:
        1. Put your keystore in ~/.android/release-keystore and set up a key "nethunter"
        2. set KEYSTORE_PASSWORD and KEY_PASSWORD environment variables before running gradle.  this
           can be done easily by adding them to .bashrc
        (or change the below values, hard-coding or whatever...)
        3. Don't forget to create a proguard.cfg file.  You can find a sample one in the sdk or
           point to it directly.
        */
        release {
            storePassword System.getenv("KEYSTORE_PASSWORD")
            storeFile file("${System.properties['user.home']}" +  "/.android/release.keystore")
            keyAlias "nethunter"
            keyPassword System.getenv("KEY_PASSWORD")

        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + getDate() + "L)"
            buildConfigField "String", "BUILD_NAME", "\"" + System.getenv("USER") + "\"";
            return true
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-project.txt')
            debuggable false
            jniDebuggable false
            zipAlignEnabled true
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + getDate() + "L)"
            buildConfigField "String", "BUILD_NAME", "\"" + System.getenv("USER") + "\"";
            return true
        }
    }
}
